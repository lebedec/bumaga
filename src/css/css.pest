WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

StyleSheet = _{ SOI ~ Rules ~ EOI }

Rules = { Rule* }

Rule = _{ Animation | Style }

// @keyframes

Animation = { "@keyframes" ~ Name ~ "{" ~ Keyframes ~ "}" }

Name = @{ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_")+ }

Keyframes = _{ Keyframe* }

Keyframe = { Step ~ DeclarationBlock }

Step = @{ (!"{" ~ ANY)+ }

// style

Style = { Selectors ~ DeclarationBlock }

Selectors = { Complex ~ ("," ~ Complex)* }

Complex = { (Combinator? ~ Compound)* }

Combinator = { ">" | "+" | "~" }

Compound = ${ Simple+ }

Simple = _{ Id | Class | PseudoClass | PseudoElement | Attribute | Type }

Id = { "#" ~ Ident }

Class = { "." ~ Ident }

Type = { Ident }

Attribute = {
    "[" ~ Ident ~ "]" |
    "[" ~ Ident ~ Matcher ~ (String | Ident) ~ Modifier? ~ "]"
}

Matcher = { ("~" | "|" | "^" | "$" | "*")? ~ "=" }

Modifier = { "i" | "s" }

PseudoClass = {
    ":" ~ Ident ~ "(" ~ (!")" ~ ANY)* ~ ")" |
    ":" ~ Ident
}

PseudoElement = { "::" ~ Ident }

Arguments = @{ (!")" ~ ASCII_ALPHA)* }

// properties

DeclarationBlock = {
    "{" ~ "}" |
    "{" ~ Declaration* ~ "}"
}

Declaration = { Property ~ ":" ~ Values ~ ";" }

Property = { (ASCII_ALPHA | "-" | "_")+ }

Ident = _{ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_")+ }

String = @{"\"" ~ (!"\"" ~ ANY)* ~ "\"" }

Values = { Shorthand ~ ("," ~ Shorthand)* }

Shorthand = ${ Value ~ (WHITESPACE ~ Value)* }

Value = _{  Var | Calc |  Rgba | Rgb | Color |  Keyword | Percentage | Dimension | Number | Zero | Raw }

Rgba = @{ "rgba(" ~ (ASCII_DIGIT+ ~ "," ~ WHITESPACE*){3} ~ Number ~ ")" }

Rgb = @{ "rgb(" ~ (ASCII_DIGIT+ ~ "," ~ WHITESPACE*){2} ~ ASCII_DIGIT+ ~ ")" }

Color = { "#" ~ ASCII_HEX_DIGIT+ }

Zero = { "0" }

Percentage = { Number ~ "%" }

Dimension = { Number ~ Unit }

Number = { (ASCII_DIGIT | "-" | ".")+ }

Unit = { ASCII_ALPHA+ }

Var = { "var(" ~ Variable ~ ("," ~ Fallback)? ~ ")" }

Variable = { Ident }

Fallback = { Raw }

Keyword = { "-"? ~ "-"? ~ (ASCII_ALPHA | "-" | "_")+ }

Calc = @{ "calc(" ~ (!")" ~ ANY)* ~ ")"*  }

Operator = { "+" | "-" | "*" | "/" }

Raw = { (!(";" | "," | "}") ~ ANY)+ }