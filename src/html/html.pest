// https://www.w3.org/TR/2012/WD-html-markup-20120329/terminology.html#space
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "<!--" ~ (!"-->" ~ ANY)* ~ "-->" }

Document = _{ SOI ~ Content+ ~ EOI }

// TODO element array

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
Content = _{ Script | Void | Text | Element  }

Script = { "<script>" ~ (!"<" ~ ANY)* ~ "</script>" }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
Element = { "<" ~ Tag ~ Attributes ~">" ~ Children ~ "<" ~ "/" ~  Tag ~ ">"}

Children = { Content* }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
Void = { "<" ~ VoidTag ~ Attributes ~ ("/")? ~ ">" }

VoidTag = {
    ^"area"
    | ^"base"
    | ^"br"
    | ^"col"
    | ^"command"
    | ^"embed"
    | ^"hr"
    | ^"img"
    | ^"input"
    | ^"keygen"
    | ^"link"
    | ^"meta"
    | ^"param"
    | ^"source"
    | ^"track"
    | ^"wbr"
}

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
Tag = @{ TChar+ }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
TChar = { ASCII_ALPHA | ASCII_DIGIT }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-elements
Text = { (Binder | String)+  }

String = @{ SChar+ }

SChar = {
    !("<" | ">" | "{") ~ ANY
}

Attributes = { Attribute* }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
Attribute = _{
    RepeatBinding
    | AliasBinding
    | AttributeBinding
    | CallbackBinding
    | VisibilityBinding
    | DoubleQuoted
    | Unquoted
    | Empty
    }

AliasBinding = { "{" ~ Name ~ "}" ~ "=" ~ Binder }

AttributeBinding = { "[" ~ Name ~ "]" ~ "=" ~ Binder }

CallbackBinding = { "[" ~ Name ~ "]" ~ "~" ~ Key ~ "=" ~ Binder }

VisibilityBinding = { Visible ~ "=" ~ Binder }

Visible = { "?" | "!" }

RepeatBinding = { "{" ~ Name ~ "}" ~ "~" ~ Count ~ "=" ~ Binder }

Count = { ASCII_DIGIT+ }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
DoubleQuoted = { Name ~ "=" ~ "\"" ~ Value ~ "\"" }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
Unquoted = { Name ~ "=" ~ Value }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
Empty = { Name }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
Name = @{ NChar+ }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
NChar = @{
    !("\"" | "\\" | "'" | ">" | "/" | "=" | "\u{0000}" | WHITESPACE) ~ ANY
}

Binder = { "{" ~ Getter ~ ("|" ~ Transformer)* ~ "}" }

Getter = { Key ~ ("." ~ Key)* }

Transformer = { Key }

Key = @{ TChar+ }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
Value = @{ VChar* }

// https://www.w3.org/TR/2012/WD-html-markup-20120329/syntax.html#syntax-attributes
VChar = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
